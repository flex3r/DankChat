apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-kapt"
apply plugin: 'dagger.hilt.android.plugin'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"
    ndkVersion "21.3.6528147"
    defaultConfig {
        applicationId "com.flxrs.dankchat"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 161
        versionName "2.0.7"
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    buildFeatures {
        dataBinding = true
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [applicationLabel: "@string/app_name"]
        }
        debug {
            manifestPlaceholders = [applicationLabel: "@string/app_name"]
        }
        dank {
            initWith debug
//            minifyEnabled true
//            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [applicationLabel: "@string/app_name_dank"]
            applicationIdSuffix ".dank"
        }
    }
    applicationVariants.all { variant ->
        outputs.all {
            outputFileName = "$apkName-${variant.name}.apk"
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += [
                "-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi",
                "-Xuse-experimental=kotlinx.coroutines.FlowPreview"
        ]
    }
    packagingOptions {
        exclude 'META-INF/kotlinx-io.kotlin_module'
        exclude 'META-INF/atomicfu.kotlin_module'
        exclude 'META-INF/kotlinx-coroutines-io.kotlin_module'
        exclude 'META-INF/kotlinx-coroutines-core.kotlin_module'
    }
}

dependencies {
// D8 desugaring
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.0.10'

// Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

// AndroidX
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.browser:browser:$browser_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraint_version"
    implementation "androidx.core:core-ktx:$core_version"
    implementation "androidx.emoji:emoji:$emoji_version"
    implementation "androidx.exifinterface:exifinterface:$exif_version"
    implementation "androidx.fragment:fragment:$fragment_version"
    implementation "androidx.fragment:fragment-ktx:$fragment_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0' // TODO Deprecated in newer version, switch to other observable pattern in the future
    implementation "androidx.media:media:$media_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation "androidx.preference:preference-ktx:$preference_version"
    implementation "androidx.recyclerview:recyclerview:$recyclerview_version"
    implementation "androidx.viewpager2:viewpager2:$viewpager2_version"
    implementation "com.google.android.material:material:$material_version"
    implementation "androidx.security:security-crypto:$security_version"

// Dependency injection
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"
    implementation 'androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha02'
    implementation 'androidx.hilt:hilt-common:1.0.0-alpha02'
    kapt 'androidx.hilt:hilt-compiler:1.0.0-alpha02'

// Image loading
    implementation "io.coil-kt:coil:$coil_version"

// GIF support
    implementation "pl.droidsonroids.gif:android-gif-drawable:$gif_drawable_version"

// Detect and normalize URLs in text
    implementation "io.github.url-detector:url-detector:$url_detector_version"

// HTTP clients
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
}
