apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt"
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlinx-serialization'

android {
    compileSdkVersion 33
    buildToolsVersion "33.0.0"
    defaultConfig {
        applicationId "com.flxrs.dankchat"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 30414
        versionName "3.4.14"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    signingConfigs {
        release {
            def tmpFilePath = System.getProperty("user.home") + "/dankchat/"
            def allFilesFromDir = new File(tmpFilePath).listFiles()

            if (allFilesFromDir != null) {
                def keystoreFile = allFilesFromDir.first()
                keystoreFile.renameTo("keystore/DankChat.jks")
            }

            storeFile = file("keystore/DankChat.jks")
            storePassword System.getenv("SIGNING_STORE_PASSWORD")
            keyAlias System.getenv("SIGNING_KEY_ALIAS")
            keyPassword System.getenv("SIGNING_KEY_PASSWORD")
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    buildFeatures {
        dataBinding = true
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [applicationLabel: "@string/app_name"]
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [applicationLabel: "@string/app_name"]
        }
        dank {
            initWith debug
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules-dank.pro'
            manifestPlaceholders = [applicationLabel: "@string/app_name_dank"]
            applicationIdSuffix ".dank"
        }
    }
    applicationVariants.all { variant ->
        outputs.all {
            outputFileName = "$apkName-${variant.name}.apk"
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = "11"
        freeCompilerArgs += [
                "-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi",
                "-opt-in=kotlinx.coroutines.FlowPreview",
                "-opt-in=kotlinx.serialization.ExperimentalSerializationApi"
        ]
        languageVersion = "1.8"
    }
    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }
    namespace 'com.flxrs.dankchat'
}

dependencies {
// D8 desugaring
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

// Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$serialization_version"

// AndroidX
    implementation "androidx.activity:activity-ktx:$activity_version"
    implementation "androidx.browser:browser:$browser_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraint_version"
    implementation "androidx.core:core-ktx:$core_version"
    implementation "androidx.emoji2:emoji2:$emoji_version"
    implementation "androidx.exifinterface:exifinterface:$exif_version"
    implementation "androidx.fragment:fragment-ktx:$fragment_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.media:media:$media_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation "androidx.preference:preference-ktx:$preference_version"
    implementation "androidx.recyclerview:recyclerview:$recyclerview_version"
    implementation "androidx.viewpager2:viewpager2:$viewpager2_version"
    implementation "androidx.webkit:webkit:$webkit_version"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

// Material
    implementation "com.google.android.material:material:$material_version"


// Dependency injection
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

// Image loading
    implementation "io.coil-kt:coil:$coil_version"
    implementation "io.coil-kt:coil-gif:$coil_version"
    implementation "pl.droidsonroids.gif:android-gif-drawable:$gif_drawable_version"

// HTTP clients
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    implementation "io.ktor:ktor-client-core:$ktor_version"
    implementation "io.ktor:ktor-client-okhttp:$ktor_version"
    implementation "io.ktor:ktor-client-logging:$ktor_version"
    implementation "io.ktor:ktor-client-content-negotiation:$ktor_version"
    implementation "io.ktor:ktor-serialization-kotlinx-json:$ktor_version"

    // Test
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_ersion"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:$junit_ersion"
    testImplementation "io.mockk:mockk:$mockk_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"


// Localization
//    implementation "com.github.crowdin.mobile-sdk-android:sdk:$crowdin_version"

// Debug
    //dankImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanary_version"
    //dankImplementation "com.github.chuckerteam.chucker:library:$chucker_version"
    //debugImplementation "com.github.chuckerteam.chucker:library-no-op:$chucker_version"
    //releaseImplementation "com.github.chuckerteam.chucker:library-no-op:$chucker_version"
}
